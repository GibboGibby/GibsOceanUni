#pragma kernel TimeAmplitudeSpectrum

RWTexture2D<float2> DxDz;
RWTexture2D<float2> DyDxz;
RWTexture2D<float2> DyxDyz;
RWTexture2D<float2> DxxDzz;

Texture2D<float4> H0;
//Wave vector matrix, 1 / magnitude, wave vector z, frequency
Texture2D<float4> WaveData;
float Time;

float2 ComplexMult(float2 a, float2 b)
{
    return float2(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

[numthreads(8,8,1)]
void TimeAmplitudeSpectrum(uint3 id : SV_DispatchThreadID)
{
    float4 wave = WaveData[id.xy];
    float phase = wave.w * Time;
    float2 exponent = float2(cos(phase), sin(phase));
    float2 h = ComplexMult(H0[id.xy].xy, exponent)
        + ComplexMult(H0[id.xy].zw, float2(exponent.x, -exponent.y));
    float2 ih = float2(-h.y, h.x);
	
    float2 displacementX = ih * wave.x * wave.y;
    float2 displacementY = h;
    float2 displacementZ = ih * wave.z * wave.y;
		 
    float2 displacementX_dx = -h * wave.x * wave.x * wave.y;
    float2 displacementY_dx = ih * wave.x;
    float2 displacementZ_dx = -h * wave.x * wave.z * wave.y;
		 
    float2 displacementY_dz = ih * wave.z;
    float2 displacementZ_dz = -h * wave.z * wave.z * wave.y;
	
    DxDz[id.xy] = float2(displacementX.x - displacementZ.y, displacementX.y + displacementZ.x);
    DyDxz[id.xy] = float2(displacementY.x - displacementZ_dx.y, displacementY.y + displacementZ_dx.x);
    DyxDyz[id.xy] = float2(displacementY_dx.x - displacementY_dz.y, displacementY_dx.y + displacementY_dz.x);
    DxxDzz[id.xy] = float2(displacementX_dx.x - displacementZ_dz.y, displacementX_dx.y + displacementZ_dz.x);
}